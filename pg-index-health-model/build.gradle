plugins {
    id "info.solidsoft.pitest"
}

description = "pg-index-health-model is a set of common classes and interfaces for getting information about PostgreSQL database objects."

dependencies {
    testImplementation("nl.jqno.equalsverifier:equalsverifier:${equalsverifierVersion}")
    testImplementation("org.mockito:mockito-core:${mockitoVersion}")

    testFixturesImplementation("com.google.code.findbugs:jsr305:${jsr305Version}")

    pitest("it.mulders.stryker:pit-dashboard-reporter:${pitDashboardReporterVersion}")
}

test {
    testLogging.showStandardStreams = false // set to true for debug purposes
    useJUnitPlatform()
    dependsOn checkstyleMain, checkstyleTest, checkstyleTestFixtures, pmdMain, pmdTest, pmdTestFixtures, spotbugsMain, spotbugsTest, spotbugsTestFixtures
    maxParallelForks = customMaxParallelForks
    finalizedBy jacocoTestReport
    finalizedBy jacocoTestCoverageVerification
}

pitest {
    junit5PluginVersion = "1.1.2"
    pitestVersion = "1.10.4"
    threads = 4
    outputFormats = System.getenv("STRYKER_DASHBOARD_API_KEY") != null ? ["stryker-dashboard"] : ["HTML"]
    timestampedReports = false
    mutationThreshold = 100
}

tasks.named("pitest").configure {
    mustRunAfter test
}

build.dependsOn "pitest"
