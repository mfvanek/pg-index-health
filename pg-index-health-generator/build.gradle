plugins {
    id 'info.solidsoft.pitest'
}

description = 'pg-index-health-generator is an extension for generating database migrations in sql format based on pg-index-health diagnostics.'

dependencies {
    api project(':pg-index-health-model')
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"

    testImplementation(testFixtures(project(":pg-index-health-model")))
    testImplementation(testFixtures(project(":pg-index-health-jdbc-connection")))
    testImplementation "ch.qos.logback:logback-classic:${logbackVersion}"

    pitest "it.mulders.stryker:pit-dashboard-reporter:${pitDashboardReporterVersion}"
}

test {
    testLogging.showStandardStreams = false // set to true for debug purposes
    useJUnitPlatform()
    dependsOn checkstyleMain, checkstyleTest, pmdMain, pmdTest, spotbugsMain, spotbugsTest
    maxParallelForks = customMaxParallelForks
    finalizedBy jacocoTestReport
    finalizedBy jacocoTestCoverageVerification
}

pitest {
    junit5PluginVersion = '1.1.2'
    pitestVersion = '1.10.4'
    threads = 4
    outputFormats = System.getenv('STRYKER_DASHBOARD_API_KEY') != null ? ['stryker-dashboard'] : ['HTML']
    timestampedReports = false
    mutationThreshold = 96
}

tasks.named('pitest').configure {
    mustRunAfter test
}

build.dependsOn 'pitest'
